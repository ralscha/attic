<?xml version="1.0" encoding="utf-8"?>
<mx:Panel xmlns:mx="http://www.adobe.com/2006/mxml" width="626" title="Art" 
  layout="absolute" creationComplete="init()">
  
  <mx:Script>
    <![CDATA[
      import mx.events.ListEvent;
      import mx.messaging.events.ChannelEvent;
      import ch.ess.hgtracker.db.Club;
      import mx.managers.PopUpManager;
      import mx.events.CloseEvent;
      import mx.controls.Alert;
      import mx.collections.SortField;
      import mx.collections.Sort;
      import mx.collections.ArrayCollection;
      import mx.rpc.events.ResultEvent;
      import ch.ess.hgtracker.db.Art;      
      
      [Bindable]
      [ArrayElementType("ch.ess.hgtracker.db.Art")]
      private var artModel:ArrayCollection = new ArrayCollection();
      
      public var club:Club;
      
      private var selectedArt:Art;
      
      private function edit():void {        
        var artEdit:ArtEdit = PopUpManager.createPopUp(this, ArtEdit, true) as ArtEdit;
        artEdit.editArt = selectedArt; 
        artEdit.club = club;
        artEdit.addEventListener("dataInserted", onDataInserted);   
      }
            
      private function deleteArt():void {
        
        Alert.show('Diese Art wirklich löschen?', 'Frage', (Alert.YES | Alert.NO), this,
                    function(event:CloseEvent):void {
                      if (event.detail == Alert.YES) {
                        artService.deleteArt(selectedArt.id);
                        
                        artDataGrid.selectedIndex = -1;
                        artDataGrid.selectedItem = null;
                        selectedArt = null;
                                
                        artService.getAll(club);
                      }
                    }
                );

      }
      
      private function neu():void {        
        
        artDataGrid.selectedIndex = -1;
        artDataGrid.selectedItem = null;
        editButton.enabled = false;
        deleteButton.enabled = false; 
        
        selectedArt = new Art();
        selectedArt.id = -1; 
        
        var artEdit:ArtEdit = PopUpManager.createPopUp(this, ArtEdit, true) as ArtEdit;
        artEdit.editArt = selectedArt;
        artEdit.club = club;          
        artEdit.addEventListener("dataInserted", onDataInserted);       
      }
      
      public function init():void {
        artService.getAll(club);
      }
      
      private function onChange(e:ListEvent):void {
        editButton.enabled = true;
        selectedArt = Art(artDataGrid.selectedItem);
        
        if (selectedArt.anzahlSpiele == 0) {
          deleteButton.enabled = true;
        } else {
          deleteButton.enabled = false;
        }  
     
      }
      
      private function onGetAllResult(event:ResultEvent):void {
                
        var artSort:Sort = (artDataGrid.dataProvider ? ArrayCollection(artDataGrid.dataProvider).sort : null);
        artModel = ArrayCollection(event.result);

        if (artSort) {
          artModel.sort = artSort;
               
        } else {
          var sort:Sort = new Sort();
          sort.fields = [new SortField("spielArt", true)];
          
          artModel.sort = sort;
        }    
        artModel.refresh();                
      }   
            
      private function onDataInserted(event:Event):void {
        //Alert.show("Daten gespeichert", "Info");  
        artService.getAll(club);
      }      
      
      
    ]]>
  </mx:Script>
  
  <mx:RemoteObject id="artService" showBusyCursor="true" destination="artService">
    <mx:method name="getAll" result="onGetAllResult(event)" />
  </mx:RemoteObject>
  
  <mx:DataGrid id="artDataGrid" dataProvider="{artModel}" 
    change="onChange(event)" bottom="60" top="10" left="10" right="10">
    <mx:columns>
      <mx:DataGridColumn headerText="Art Name" dataField="spielArt"/>
      <mx:DataGridColumn headerText="Anzahl Spiele" dataField="anzahlSpiele" width="100" textAlign="right" paddingRight="5"/>
    </mx:columns>
  </mx:DataGrid>
  <mx:HBox left="10" bottom="20" right="10">
    <mx:Button label="Neue Art" id="newButton" click="neu()" icon="@Embed(source='assets/form_green_add.png')"/>
    <mx:Button label="Art editieren" id="editButton" enabled="false" click="edit()" icon="@Embed(source='assets/form_green_edit.png')"/>
    <mx:Spacer width="100%" />
    <mx:Button label="Art löschen" id="deleteButton" enabled="false" click="deleteArt()" icon="@Embed(source='assets/form_green_delete.png')"/>
  </mx:HBox>
</mx:Panel>
